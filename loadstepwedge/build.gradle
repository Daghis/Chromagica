plugins {
    id 'application'
    id 'distribution'

    id 'java'
    id 'jacoco'

    id 'idea'
}

group = 'net.bluevine'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'com.squareup.moshi:moshi:1.15.1'
    implementation 'com.squareup.moshi:moshi-adapters:1.15.1'

    implementation 'com.google.guava:guava:33.2.1-jre'
    implementation 'com.google.flogger:flogger:0.8'
    implementation 'com.google.flogger:flogger-system-backend:0.8'

    implementation 'info.picocli:picocli:4.7.6'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'

    implementation 'me.tongfei:progressbar:0.10.1'

    implementation 'org.apache.commons:commons-lang3:3.15.0'
    implementation 'org.apache.commons:commons-math3:3.6'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
}

distributions {
    main {
        contents {
            into('lib') {
                from configurations.runtimeClasspath
                from tasks.jar
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(Test).configureEach {
    reports {
        junitXml.required = true
        html.required = true
    }
}

application {
    mainClass = 'net.bluevine.chromagica.loadstepwedge.LoadStepWedge'
}

tasks.jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

tasks.distTar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName.set('Chromagica')
    archiveVersion.set(version) // Dynamically apply version
}

tasks.distZip {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName.set('Chromagica')
    archiveVersion.set(version) // Dynamically apply version
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
