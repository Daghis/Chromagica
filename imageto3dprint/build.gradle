plugins {
    id 'application'
    id 'distribution'

    id 'java'
    id 'jacoco'

    id 'idea'
}

group = 'net.bluevine'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'com.google.guava:guava:33.2.1-jre'

    implementation 'info.picocli:picocli:4.7.6'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'

    implementation 'me.tongfei:progressbar:0.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(Test).configureEach {
    reports {
        junitXml.required = true
        html.required = true
    }
}

application {
    mainClass = 'net.bluevine.chromagica.imageto3dprint.ImageTo3dPrint'
}

distributions {
    main {
        contents {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            into('lib') {
                from configurations.runtimeClasspath
                from tasks.jar
            }
        }
    }
}

tasks.jar {
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
}

tasks.distTar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName.set('Chromagica')
    archiveVersion.set(version) // Dynamically apply version
}

tasks.distZip {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName.set('Chromagica')
    archiveVersion.set(version) // Dynamically apply version
}

tasks.named('compileJava') {
    dependsOn(':generateVersionClass')
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

sourceSets {
    main {
        java {
            // Add the generated sources from the root project
            srcDir("$rootProject.buildDir/generated/src/main/java")
        }
    }
}
